apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "$project.rootDir/script-git-version.gradle"

android {
    compileSdkVersion Integer.parseInt(compile_sdk_version)
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId 'ru.erdenian.studentassistant'
        versionCode gitVersionCode
        versionName gitVersionName

        minSdkVersion 21
        targetSdkVersion Integer.parseInt(target_sdk_version)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = outputFileName.replace('.apk', "-${variant.versionName}.apk")
        }
    }

    // Разрешение конфликта зависимостей, возникшего при обновлении Guava до версии 22.0-android
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets { main.java.srcDirs += 'src/main/kotlin' }
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$support_libraries_version"
    implementation "com.android.support:design:$support_libraries_version"
    implementation "com.android.support:cardview-v7:$support_libraries_version"

    implementation project(':schedule')
    implementation project(':customviews')

    implementation 'org.jetbrains.anko:anko-common:0.10.5'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'com.google.guava:guava:25.0-android'

    implementation 'com.github.ceryle:SegmentedButton:v2.0.2'
}
