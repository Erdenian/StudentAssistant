apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "$project.rootDir/script-git-version.gradle"

buildscript {
    repositories { jcenter() }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion buildTools_version

    defaultConfig {
        applicationId 'ru.erdenian.studentassistant'
        versionCode gitVersionCode
        versionName gitVersionName
        minSdkVersion 16
        targetSdkVersion 25

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each {
            it.outputFile = new File(it.outputFile.parent, it.outputFile.name.replace('.apk', "-${variant.versionName}.apk"))
        }
    }

    // Разрешение конфликта зависимостей, возникшего при обновлении Guava до версии 22.0-android
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:2.0.1'
    }

    sourceSets { main.java.srcDirs += 'src/main/kotlin' }

    return void
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:appcompat-v7:$supportLibraries_version"
    compile "com.android.support:design:$supportLibraries_version"
    compile "com.android.support:cardview-v7:$supportLibraries_version"

    compile project(':schedule')
    compile project(':customviews')

    compile 'org.jetbrains.anko:anko-common:0.10.1'
    compile 'joda-time:joda-time:2.9.9'
    compile 'com.google.guava:guava:22.0-android'

    compile 'com.code-troopers.betterpickers:library:3.1.0'
    compile 'com.github.ceryle:SegmentedButton:v1.2.2'
}
